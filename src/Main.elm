module Main exposing (..)

import Array
import Browser exposing (Document)
import Element exposing (Element, alignRight, centerX, centerY, column, el, fill, padding, px, rgb255, row, spacing, text, width)
import Element.Background as Background
import Element.Border as Border
import Element.Font as Font
import Element.Input as Input
import Html exposing (Html)
import Random
import Time


questions =
    Array.fromList
        [ q Der "Hund" "ðŸ¶"
        , q Die "Katze" "ðŸ±"
        , q Das "Pferd" "ðŸ´"
        , q Die "Schlange" "ðŸ"
        , q Das "Huhn" "ðŸ”"
        , q Der "Elefant" "ðŸ˜"
        , q Das "Wildschwein" "ðŸ—"
        , q Die "Schnecke" "ðŸŒ"
        , q Die "Biene" "ðŸ"
        , q Der "MarienkÃ¤fer" "ðŸž"
        , q Der "Fisch" "ðŸŸ"
        , q Die "SchildkrÃ¶te" "ðŸ¢"
        , q Das "KÃ¼ken" "ðŸ£"
        , q Der "Pinguin" "ðŸ§"
        , q Das "Kamel" "ðŸ«"
        , q Der "Delfin" "ðŸ¬"
        , q Die "Maus" "ðŸ­"
        , q Die "Brille" "ðŸ‘“"
        , q Die "Hose" "ðŸ‘–"
        , q Der "Baum" "ðŸŒ³"
        , q Der "Kaktus" "ðŸŒµ"
        , q Das "Blatt" "ðŸƒ"
        , q Die "Melone" "ðŸ‰"
        , q Die "Zitrone" "ðŸ‹"
        , q Die "Banane" "ðŸŒ"
        , q Die "Ananas" "ðŸ"
        , q Der "Apfel" "ðŸŽ"
        , q Die "Birne" "ðŸ"
        , q Der "Pfirsich" "ðŸ‘"
        , q Die "Erdbeere" "ðŸ“"
        , q Die "Kiwi" "ðŸ¥"
        , q Die "Tomate" "ðŸ…"
        , q Die "Kartoffel" "ðŸ¥”"
        , q Die "Karotte" "ðŸ¥•"
        , q Der "Mais" "ðŸŒ½"
        , q Die "Gurke" "ðŸ¥’"
        , q Der "Pilz" "ðŸ„"
        , q Das "Brot" "ðŸž"
        , q Das "Croissant" "ðŸ¥"
        , q Der "KÃ¤se" "ðŸ§€"
        , q Das "Fleisch" "ðŸ—"
        , q Die "Pizza" "ðŸ•"
        , q Das "Ei" "ðŸ¥š"
        , q Der "Salat" "ðŸ¥—"
        , q Das "Popcorn" "ðŸ¿"
        , q Der "Reis" "ðŸš"
        , q Die "Nudelsuppe" "ðŸœ"
        , q Das "Eis" "ðŸ¨"
        , q Der "Keks" "ðŸª"
        , q Der "Kuchen" "ðŸŽ‚"
        , q Die "Torte" "ðŸ°"
        , q Die "Schokolade" "ðŸ«"
        , q Das "Bonbon" "ðŸ¬"
        , q Der "Lolli" "ðŸ­"
        , q Der "Honig" "ðŸ¯"
        , q Die "Milch" "ðŸ¥›"
        , q Der "Kaffee" "â˜•"
        , q Der "Sekt" "ðŸ¾"
        , q Der "Wein" "ðŸ·"
        , q Das "Bier" "ðŸº"
        , q Das "Besteck" "ðŸ´"
        , q Der "LÃ¶ffel" "ðŸ¥„"
        , q Das "Messer" "ðŸ”ª"
        , q Die "Erde" "ðŸŒ"
        , q Der "Berg" "â›°"
        , q Der "Vulkan" "ðŸŒ‹"
        , q Das "Haus" "ðŸ "
        , q Das "Krankenhaus" "ðŸ¥"
        , q Die "Bank" "ðŸ¦"
        , q Die "Kirche" "â›ª"
        , q Die "Lok" "ðŸš‚"
        , q Der "Zug" "ðŸš„"
        , q Die "Eisenbahn" "ðŸš†"
        , q Die "U-Bahn" "ðŸš‡"
        , q Die "StraÃŸenbahn" "ðŸš‹"
        , q Der "Bus" "ðŸšŒ"
        , q Das "Auto" "ðŸš—"
        , q Der "Lastwagen" "ðŸš›"
        , q Das "Fahrrad" "ðŸš²"
        , q Das "Motorrad" "ðŸ"
        , q Die "Ampel" "ðŸš¦"
        , q Das "Schild" "ðŸ›‘"
        , q Der "Anker" "âš“"
        , q Das "Boot" "ðŸ›¶"
        , q Das "Schiff" "ðŸš¢"
        , q Das "Flugzeug" "âœˆ"
        , q Der "Hubschrauber" "ðŸš"
        , q Die "Rakete" "ðŸš€"
        , q Die "TÃ¼r" "ðŸšª"
        , q Das "Bett" "ðŸ›Œ"
        , q Das "Klo" "ðŸš½"
        , q Die "Toilette" "ðŸš½"
        , q Die "Dusche" "ðŸš¿"
        , q Die "Badewanne" "ðŸ›"
        , q Die "Armbanduhr" "âŒš"
        , q Der "Wecker" "â°"
        , q Die "Uhr" "ðŸ•‘"
        , q Der "Mond" "ðŸŒ›"
        , q Das "Thermometer" "ðŸŒ¡"
        , q Die "Temperatur" "ðŸŒ¡"
        , q Der "Stern" "â­"
        , q Die "Wolke" "â˜"
        , q Das "Wetter" "â›…"
        , q Das "Gewitter" "â›ˆ"
        , q Der "Regen" "ðŸŒ§"
        , q Der "Regenbogen" "ðŸŒˆ"
        , q Der "Regenschirm" "â˜‚"
        , q Der "Blitz" "âš¡"
        , q Der "Schnee" "ðŸŒ¨"
        , q Die "Schneeflocke" "â„"
        , q Das "Feuer" "ðŸ”¥"
        , q Das "Wasser" "ðŸ’§"
        , q Der "Tropfen" "ðŸ’§"
        , q Die "Welle" "ðŸŒŠ"
        , q Der "Luftballon" "ðŸŽˆ"
        , q Das "Geschenk" "ðŸŽ"
        , q Der "Ball" "âš½"
        , q Der "WÃ¼rfel" "ðŸŽ²"
        , q Die "Glocke" "ðŸ””"
        , q Die "Musik" "ðŸŽ¶"
        , q Das "Saxophon" "ðŸŽ·"
        , q Die "Gitarre" "ðŸŽ¸"
        , q Das "Klavier" "ðŸŽ¹"
        , q Die "Trompete" "ðŸŽº"
        , q Die "Geige" "ðŸŽ»"
        , q Die "Trommel" "ðŸ¥"
        , q Das "Handy" "ðŸ“±"
        , q Das "Telefon" "â˜Ž"
        , q Die "Batterie" "ðŸ”‹"
        , q Der "Stecker" "ðŸ”Œ"
        , q Der "Computer" "ðŸ’»"
        , q Der "Film" "ðŸŽ¬"
        , q Die "Lupe" "ðŸ”"
        , q Die "Kerze" "ðŸ•¯"
        , q Die "GlÃ¼hbirne" "ðŸ’¡"
        , q Die "Taschenlampe" "ðŸ”¦"
        , q Das "Buch" "ðŸ“–"
        , q Das "Papier" "ðŸ“„"
        , q Die "Zeitung" "ðŸ“°"
        , q Das "Geld" "ðŸ’¶"
        , q Die "Kreditkarte" "ðŸ’³"
        , q Der "Briefkasten" "ðŸ“®"
        , q Der "Bleistift" "âœ"
        , q Der "Stift" "ðŸ–"
        , q Der "Kugelschreiber" "ðŸ–Š"
        , q Der "Kalender" "ðŸ“…"
        , q Das "Lineal" "ðŸ“"
        , q Die "Schere" "âœ‚"
        , q Das "Schloss" "ðŸ”’"
        , q Der "SchlÃ¼ssel" "ðŸ”‘"
        , q Der "Hammer" "ðŸ”¨"
        , q Der "Einkaufswagen" "ðŸ›’"
        , q Die "Fahne" "ðŸ´"
        ]


defaultQuestion =
    q Der "Hund" "ðŸ¶"


main =
    Browser.document { init = init, view = view, update = update, subscriptions = subscriptions }


subscriptions model =
    Time.every 1000 Tick


type Model
    = InGame InGameModel
    | Welcome
    | GameOver Score


type alias InGameModel =
    { question : Question
    , score : Int
    , errors : List Article
    , remainingSeconds : Int
    }


type alias Score =
    Int


type Msg
    = Answer Article
    | NewQuestion Int
    | Tick Time.Posix
    | StartGame


type Article
    = Der
    | Die
    | Das


type alias Question =
    { noun : String, article : Article, picture : String }


q : Article -> String -> String -> Question
q article noun picture =
    { article = article, noun = noun, picture = picture }


init : () -> ( Model, Cmd Msg )
init _ =
    ( Welcome, Cmd.none )


startGame =
    ( InGame
        { question = defaultQuestion
        , score = 0
        , errors = []
        , remainingSeconds = 60
        }
    , generateNewQuestion
    )


generateNewQuestion =
    Random.generate NewQuestion (Random.int 0 (Array.length questions - 1))


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case model of
        InGame m ->
            updateInGame msg m

        Welcome ->
            updateWelcome msg

        GameOver score ->
            updateGameOver msg score


updateInGame : Msg -> InGameModel -> ( Model, Cmd Msg )
updateInGame msg model =
    case msg of
        Answer article ->
            if article == model.question.article then
                ( if model.errors == [] then
                    InGame { model | score = model.score + 1 }

                  else
                    InGame model
                , Random.generate NewQuestion (Random.int 0 (Array.length questions - 1))
                )

            else if List.member article model.errors then
                ( InGame model, Cmd.none )

            else
                ( InGame { model | errors = article :: model.errors, score = reduceScore model.score }
                , Cmd.none
                )

        NewQuestion index ->
            let
                newQuestion =
                    Array.get index questions |> Maybe.withDefault defaultQuestion
            in
            ( InGame { model | question = newQuestion, errors = [] }
            , if newQuestion == model.question then
                generateNewQuestion

              else
                Cmd.none
            )

        Tick _ ->
            if model.remainingSeconds == 0 then
                ( GameOver model.score, Cmd.none )

            else
                ( InGame { model | remainingSeconds = model.remainingSeconds - 1 }, Cmd.none )

        StartGame ->
            -- shouldn't be happening
            startGame


reduceScore score =
    if score - 2 <= 0 then
        0

    else
        score - 2


updateWelcome : Msg -> ( Model, Cmd Msg )
updateWelcome msg =
    case msg of
        StartGame ->
            startGame

        _ ->
            ( Welcome, Cmd.none )


updateGameOver : Msg -> Score -> ( Model, Cmd Msg )
updateGameOver msg score =
    case msg of
        StartGame ->
            startGame

        _ ->
            ( GameOver score, Cmd.none )


view : Model -> Document Msg
view model =
    { title = "der, die, das"
    , body = [ viewBody model ]
    }


viewBody : Model -> Html Msg
viewBody model =
    Element.layout [] <|
        case model of
            Welcome ->
                viewWelcome

            InGame inGameModel ->
                viewInGame inGameModel

            GameOver score ->
                viewGameOver score


viewAttributes =
    [ centerX
    , centerY
    , spacing 30
    , padding 30
    , Border.rounded 4
    , Border.width 1
    , Border.color darkGreen
    , Background.color lightGreen
    , width (px 400)
    ]


viewWelcome =
    column
        viewAttributes
        [ el [ centerX, Font.size 50 ] (text "Willkommen!")
        , text "Kannst du schon der, die, das?"
        , startButton "Start"
        ]


viewGameOver score =
    column
        viewAttributes
        [ el [ centerX, Font.size 50 ] (text <| String.fromInt score ++ " Punkte")
        , text "Schaffst du noch mehr?"
        , startButton "Nochmal"
        ]


startButton label =
    Input.button
        [ Background.color darkGreen
        , Font.color <| rgb255 255 255 255
        , Border.rounded 4
        , padding 10
        , centerX
        ]
        { onPress = Just StartGame, label = text label }


darkRed =
    rgb255 128 0 0


darkGreen =
    rgb255 0 128 0


lightGreen =
    rgb255 220 255 220


viewInGame : InGameModel -> Element Msg
viewInGame { question, score, errors, remainingSeconds } =
    column
        viewAttributes
        [ topBar score remainingSeconds
        , bigPicture question.picture
        , buttonRow errors
        , nounText question.noun
        ]


topBar score seconds =
    row [ width fill, spacing 30 ]
        [ el [] (text <| String.fromInt seconds ++ "s")
        , el [ alignRight ] (text <| String.fromInt score ++ " P")
        ]


bigPicture emoji =
    el [ centerX, Font.size 100 ] (text emoji)


nounText noun =
    el [ centerX, Font.size 50 ] (text noun)


buttonRow errors =
    row [ spacing 30, centerX ]
        [ derButton errors
        , dieButton errors
        , dasButton errors
        ]


answerButton label article errors =
    Input.button
        [ Background.color <|
            if errors |> List.member article then
                darkRed

            else
                darkGreen
        , Font.color <| rgb255 255 255 255
        , Border.rounded 4
        , padding 10
        ]
        { onPress = Just (Answer article), label = text label }


derButton errors =
    answerButton "der" Der errors


dieButton errors =
    answerButton "die" Die errors


dasButton errors =
    answerButton "das" Das errors
